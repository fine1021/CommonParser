apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 3

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.0'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 24
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        encoding = "utf-8"
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
}

ext {
    releaseVersion = "${versionMajor}.${versionMinor}.${versionPatch}"
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    baseName = "common-parser"
    version = project.releaseVersion
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    baseName = "common-parser"
    version = project.releaseVersion
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task buildReleaseJar(dependsOn: ['compileReleaseJavaWithJavac'], type: Jar) {

    // appendix = "demo"
    baseName = "common-parser"
    version = project.releaseVersion
    // classifier = "release"

    // 后缀名
    extension = "jar"
    // 最终的 Jar 包名，如果没设置，默认为 [baseName]-[appendix]-[version]-[classifier].[extension]
    // archiveName = "common-parser.jar"

    // 需打包的资源所在的路径集
    def srcClassDir = [project.buildDir.absolutePath + "/intermediates/classes/release"];
    // 初始化资源路径集
    from srcClassDir

    // 去除路径集下部分的资源
    exclude "com/yxkang/android/commonparser/BuildConfig.class"
    exclude "**/R.class"
    exclude "**/R\$*.class"

    // 只导入资源路径集下的部分资源
    include "com/yxkang/android/commonparser/**/*.class"

    // 注: exclude include 支持可变长参数
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

publish {
    userOrg = 'fine1021'
    repoName = 'maven'
    groupId = 'com.yxkang.android'
    artifactId = 'common-parser'
    publishVersion = project.releaseVersion
    description = 'a common parser for android'
    website = 'https://github.com/fine1021/CommonParser'
    issueTracker = "${website}/issues"
    repository = "${website}.git"
    bintrayUser = properties.getProperty("bintray.user")
    bintrayKey = properties.getProperty("bintray.apikey")
    dryRun = false
}
